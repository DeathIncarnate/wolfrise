class Tigerfaust : Weapon
{
	action void A_TigerFistFire()
	{
		A_PlaySound("weapons/tiger/fire");
		A_FireProjectile("WolfRocket", 0, 1, 0, 0);
		A_Recoil(5.0);
		A_SetPitch(pitch - 4.8, SPF_INTERPOLATE);
	}
	
	action void A_TigerFistAlt()
	{
		A_PlaySound("weapons/tiger/altfire");
		A_FireProjectile("WolfRocketSuper", 0, 1, 0, 0, 0, 0);
		A_FireProjectile("WolfRocketSuper", 0, 0, 0, 0, 0, -2);
		A_FireProjectile("WolfRocketSuper", 0, 0, 0, 0, 0, 2);
		A_FireProjectile("WolfRocketSuper", -2, 0, 0, 0, 0, 0);
		A_FireProjectile("WolfRocketSuper", 2, 0, 0, 0, 0, 0);
		A_Recoil(24.0);
	}
	
	Default
	{
		//$Category Weapons
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 5;
		Weapon.AmmoGive 16;
		Weapon.AmmoType "WolfRockets";
		Weapon.AmmoType2 "WolfRockets";
		Weapon.BobStyle "InverseSmooth";
		Inventory.PickUpSound "pickups/weapongrab";
		Weapon.BobRangeX 0.0;
		Weapon.BobRangeY 0.33;
		Weapon.BobSpeed 2.0;
		Tag "Tigerfaust";
		+WEAPON.NOAUTOAIM;
	}
	
	States
	{
		Ready:
			LNCG A 1 A_WeaponReady;
			Loop;
		Select:
			TNT1 A 0 A_PlaySound("weapons/tiger/equip");
		Selecting:
			LNCG A 1 A_Raise(25);
			Loop;
		Deselect:
			LNCG A 1 A_Lower(25);
			Loop;
		Fire:
			LNCG B 1;
			LNCG C 1 A_TigerFistFire();
			LNCG D 1 A_SetPitch(pitch + 0.75, SPF_INTERPOLATE);
			LNCG AAAAAAAAAAA 1 A_SetPitch(pitch + 0.24, SPF_INTERPOLATE);
			Goto Ready;
		AltFire:
			LNCG A 1 A_CheckReload();
			TNT1 A 0 A_PlaySound("weapons/charge");
			LNCS ABCD 3 A_Recoil(0.16);
			LNCS ABCD 2 A_Recoil(0.16);
			LNCS ABCDEFG 1 A_Recoil(0.16);
			LNCS H 1 A_TigerFistAlt();
			LNCS IIII 1 A_SetPitch(pitch - 4.4, SPF_INTERPOLATE);
			LNCS IIIIIIIIIIII 1 A_SetPitch(pitch + 1.24, SPF_INTERPOLATE);
			LNCS JKLMNO 2;
			LNCG A 8;
			Goto Ready;
		Spawn:
			MACH P -1;
			Stop;
	}
}