class PatrolGuard : Actor
{
	Default
	{
		//$Category Enemies
		obituary "%o was shot by a Patrol Guard.";
		Health 20;
		Radius 21;
		Height 72;
		Mass 300;
		Speed 12;
		dropitem "SmallAmmo";
		Painchance 100;
		Maxtargetrange 1024;
		MinMissileChance 2;
		DeathSound "enemies/death2";
		SeeSound "enemies/swein";
		PainSound "UBERPAIN";
		BloodType "BloodPuff1";
		Scale 1.44;
		Monster;
		+MISSILEMORE
		+MISSILEEVENMORE
	}
	
	States
	{
		Spawn:
			WHIT A 2 A_Look;
			Loop;
		See:
			WHIT BCDE 4 A_Chase;
			Loop;
		Missile:
			WHIT F 16 A_FaceTarget;
			WHIT G 4;
			TNT1 A 0 A_PlaySound("weapons/luger/fire");
			WHIT H 4 A_CustomBulletAttack(8,0,1,4);
			Goto See;
		Pain:
			WHIT U 2 A_Pain;
			Goto See;
		Death:
			TNT1 A 0 A_GiveToTarget("WolfScore", 1000);
			WHIT U 4;
			WHIT V 4 A_Scream;
			WHIT W 4 A_NoBlocking;
			WHIT WXY 4;
			WHIT Z -1;
			Stop;
	}
}

class PatrolGuard_Hard : PatrolGuard
{
	Default
	{
		Health 35;
		Speed 18;
		Painchance 80;
		Maxtargetrange 2048;
	}
}

class Machinegunner1 : Actor
{
	Default
	{
		//$Category Enemies
		obituary "%o was shot by a Machinegunner.";
		Health 110;
		Radius 22;
		Height 68;
		Mass 300;
		Speed 14;
		dropitem "WolfRifle2";
		Painchance 100;
		Maxtargetrange 1024;
		DeathSound "UBRNZDED";
		SeeSound "UBRNZSEE";
		PainSound "UBERPAIN";
		BloodType "BloodPuff1";
		MaxStepHeight 16;
		Scale 1.33;
		Monster;
		MinMissileChance 1;
		+DONTHARMSPECIES
		+NOINFIGHTING
		+MISSILEMORE
		+MISSILEEVENMORE
	}
  
	States
	{
	Spawn:
		NZ45 A 10 A_Look;
		Loop;
	See:
		NZ45 BBCCDDEE 3 A_Chase;
		Loop;
	Missile:
		NZ45 F 1 A_PlaySound("weapons/warning");	
		NZ45 F 1 A_FaceTarget;
		NZ45 F 1 A_SpawnProjectile("NaziLaserTarget", 48, 0, 0);
		NZ45 F 1 A_FaceTarget;
		NZ45 F 1 A_SpawnProjectile("NaziLaserTarget", 48, 0, 0);
		NZ45 F 1 A_FaceTarget;
		NZ45 F 1 A_SpawnProjectile("NaziLaserTarget", 48, 0, 0);
		NZ45 F 1 A_FaceTarget;
		NZ45 F 1 A_SpawnProjectile("NaziLaserTarget", 48, 0, 0);
		NZ45 F 16 A_FaceTarget;
	Firing:
		NZ45 G 2;
		TNT1 A 0 A_PlaySound("weapons/fg42/fire");
		NZ45 H 2 A_CustomBulletAttack(4,0,1,4);
		TNT1 A 0 A_CPosRefire;
		Goto Firing;
	Pain:
		NZ45 I 3 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 A_GiveToTarget("WolfScore", 1500);
		NZ45 J 4;
		NZ45 K 4 A_Scream;
		NZ45 L 4 A_NoBlocking;
		NZ45 M 4;
		NZ45 N -1;
		Stop;
	}
}


class Machinegunner1_Hard : Machinegunner1
{
	Default
	{
		Health 180;
		Speed 18;
		Painchance 64;
		Maxtargetrange 4096;
	}
}