class SS1 : Actor
{
	Default
	{
		//$Category Enemies
		obituary "%o was riddled by an Afrika Korps Enforcer.";
		Health 125;
		Radius 24;
		Height 88;
		Mass 300;
		Speed 14;
		dropitem "MP50";
		Painchance 192;
		Maxtargetrange 2048;
		DeathSound "enemies/death3";
		SeeSound "enemies/halt";
		PainSound "UBERPAIN";
		BloodType "BloodPuff2";
		Scale 1.52;
		ReactionTime 6;
		Monster;
		+DONTHARMSPECIES
		+NOINFIGHTING
		+MISSILEMORE
		+MISSILEEVENMORE
		+CANUSEWALLS
	}
  
	States
	{
	Spawn:
		ENFR A 10 A_Look;
		Loop;
	See:
		ENFR BBCCDDEE 3 A_Chase;
		Loop;
	Missile:
		ENFR L 18 A_FaceTarget;
		TNT1 A 0 A_Jump(144, "AltFiring");
	Firing:
		ENFR M 2;
		TNT1 A 0 A_PlaySound("weapons/mp50/fire");
		ENFR N 2 A_CustomBulletAttack(8,0,1,3);
		TNT1 A 0 A_CPosRefire;
		Goto Firing;
	AltFiring:
		ENFR L 18
		{
			A_PlaySound("weapons/warning");	
			A_SpawnProjectile("NaziLaserTarget", 32, 0, -2);
			A_SpawnProjectile("NaziLaserTarget", 32, 0, -1);
			A_SpawnProjectile("NaziLaserTarget", 32, 0, 0);
			A_SpawnProjectile("NaziLaserTarget", 32, 0, 1);
			A_SpawnProjectile("NaziLaserTarget", 32, 0, 2);
		}
		ENFR M 3
		{
			A_PlaySound("weapons/mp50/shotgun");
			A_SpawnProjectile("NaziBullet", 32, 0, -2);
			A_SpawnProjectile("NaziBullet", 32, 0, -1);
			A_SpawnProjectile("NaziBullet", 32, 0, 0);
			A_SpawnProjectile("NaziBullet", 32, 0, 1);
			A_SpawnProjectile("NaziBullet", 32, 0, 2);
		}
		Goto See;
	Pain:
		ENFR K 3 A_Pain;
		Goto See;
	Death:
		TNT1 A 0
		{
			A_GiveToTarget("WolfScore", 1500);
			A_Scream();
			A_NoBlocking();
		}
		ENFR F 4 A_SpawnDebris("BloodPuff2", false, random(0.75, 1.25), random(0.75, 1.25));
		ENFR GHI 4;
		ENFR J -1;
		Stop;
	Death.ExplosiveDamage:
		TNT1 A 0
		{
			A_GiveToTarget("WolfScore", 1500);
			A_Scream();
			A_NoBlocking();
			A_SpawnDebris("BloodPuff3", false, random(1.5, 2.0), random(1.5, 2.0));
			A_SpawnDebris("BoneDebris", false, random(0.25, 0.5), random(0.25, 0.5));
			A_SpawnDebris("BoneDebris2", false, random(0.75, 1.25), random(0.75, 1.25));
			A_SpawnDebris("BoneDebris3", false, random(2.0, 4.0), random(2.0, 4.0));
			A_SpawnDebris("BoneDebris4", false, random(1.0, 2.0), random(1.0, 2.0));
			A_PlaySound("enemies/gibbed");
		}
		Stop;
	}
}

class SS1_Hard : SS1
{
	Default
	{
		Health 180;
		Speed 24;
		Painchance 75;
		ReactionTime 2;
		MaxTargetRange 4096;
	}
}