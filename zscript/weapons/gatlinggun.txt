class GatlingGun : Weapon
{
	action void A_PulseFire()
	{
		A_PlaySound("BLASTER1");
		A_FireProjectile("PulseBall", 0, 1, 0, 0, 0, 0);
	}
	
	Default
	{
		//$Category Weapons
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoGive 25;
		Weapon.AmmoType "HeavyAmmo";
		Weapon.AmmoType2 "HeavyAmmo";
		Inventory.Pickupmessage "You got the Venom Chaingun!";
		Inventory.PickUpSound "WEAPPICK";
		Weapon.BobStyle "InverseSmooth";
		//Weapon.BobRangeX 0.5;
		//Weapon.BobRangeY 0.75;
		Weapon.BobRangeX 0.16;
		Weapon.BobRangeY 0.64;
		Weapon.BobSpeed 0.48;
		Tag "Venom";
		+WEAPON.NOAUTOAIM;
	}
	
	States
	{
		Ready:
			PVEN A 1 A_WeaponReady;
			Loop;
		Select:
			PVEN A 1 A_Raise(25);
			Loop;
		Deselect:
			PVEN A 1 A_Lower(25);
			Loop;
		Fire:
			TNT1 A 0 A_PlaySound("weapons/venom/spinup");
			PVEN ABAB 2;
			PVEN ABAB 1;
		Hold:
			PVEN CD 2
			{
				A_CheckReload();
				A_FireProjectile("PlayerBullet", random(-3, 3), 1, 0, 0, 0, random(-2, 2));
				A_FireProjectile("PlayerBullet", random(-3, 3), 1, 0, 0, 0, random(-2, 2));
				A_Playsound("weapons/venom/fire");
				A_Recoil(0.72);
				A_SetPitch(pitch - 0.96, SPF_INTERPOLATE);
			}
			TNT1 A 0 A_Refire;
		SpinDown:
			TNT1 A 0 A_PlaySound("weapons/venom/spindown");
			PVEN ABABABAB 1 A_SetPitch(pitch + 0.5, SPF_INTERPOLATE);
			PVEN ABABAB 2 A_SetPitch(pitch + 0.25, SPF_INTERPOLATE);
			PVEN ABAB 3 A_SetPitch(pitch + 0.125, SPF_INTERPOLATE);
			Goto Ready;
		AltFire:
			TNT1 A 0 A_CheckReload();
			PVEN AB 1
			{
				A_TakeInventory("HeavyAmmo", 1);
				A_PulseFire();
				A_Recoil(0.32);
				A_SetPitch(pitch - 0.32, SPF_INTERPOLATE);
				A_SetAngle(angle + random(-1, 1));
			}
			TNT1 A 0 A_ReFire;
			Goto SpinDown;
		Spawn:
			GATL P -1;
			Stop;
	}
}

class SuperWeapon : Weapon
{
	action void A_SuperFireRight()
	{
		A_PlaySound("weapons/super/fire");
		A_FireProjectile("UberBall", 0, 1, 16, 0, 0, 0);
	}
	
	action void A_SuperFireLeft()
	{
		A_PlaySound("weapons/super/fire");
		A_FireProjectile("UberBall", 0, 1, -16, 0, 0, 0);
	}
	
	Default
	{
		//$Category Weapons
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoGive 25;
		Weapon.AmmoType "HeavyAmmo";
		Weapon.AmmoType2 "HeavyAmmo";
		Inventory.Pickupmessage "You got the Blut-Faust Gewehr 1000!";
		Inventory.PickUpSound "WEAPPICK";
		Weapon.BobStyle "InverseSmooth";
		//Weapon.BobRangeX 0.5;
		//Weapon.BobRangeY 0.75;
		Weapon.BobRangeX 0.16;
		Weapon.BobRangeY 0.64;
		Weapon.BobSpeed 0.48;
		Tag "Blut-Faust Gewehr 1000";
		+WEAPON.NOAUTOAIM;
	}
	
	States
	{
		Ready:
			SUPR A 1 A_WeaponReady;
			Loop;
		Select:
			SUPR A 1 A_Raise(25);
			Loop;
		Deselect:
			SUPR A 1 A_Lower(25);
			Loop;
		Fire:
			SUPR B 1;
			SUPC C 1	// FIRE
			{
				A_TakeInventory("HeavyAmmo", 1);
				A_SuperFireRight();
				A_Recoil(0.32);
				A_SetPitch(pitch - 0.32, SPF_INTERPOLATE);
				A_SetAngle(angle + random(-1, 1));
			}
			SUPR D 1;
			SUPR E 1;
			SUPC F 1;
			SUPR G 1	// FIRE
			{
				A_TakeInventory("HeavyAmmo", 1);
				A_SuperFireLeft();
				A_Recoil(0.32);
				A_SetPitch(pitch - 0.32, SPF_INTERPOLATE);
				A_SetAngle(angle + random(-1, 1));
			}
			SUPR H 1;
			SUPR A 1 A_ReFire();
			Goto Ready;
		Spawn:
			GATL P -1;
			Stop;
	}
}