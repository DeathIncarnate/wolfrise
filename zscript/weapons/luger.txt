class PistolUnsilenced : Inventory
{
	Default
	{
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
	}
}

class PistolSilenced : Inventory
{
	Default
	{
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
	}
}

class Luger : WolfWeapon
{
	action void A_LugerFire()
	{
		A_AlertMonsters(2048);
		A_PlaySound("weapons/luger/fire");
		A_FireBullets(1, 1, -1, 14, "BulletPuff1");
	}
	
	action void A_LugerSilencedFire()
	{
		A_AlertMonsters(2);
		A_CheckReload();
		A_PlaySound("weapons/luger/softfire");
		A_FireBullets(1, 1, -1, 8, "BulletPuff1");
		A_SetPitch(pitch - 0.72, SPF_INTERPOLATE);
	}

	Default
	{
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "SmallAmmo";
		Weapon.AmmoType2 "SmallAmmo";
		Inventory.PickUpSound "pickups/weapongrab";
		Weapon.BobRangeX 0.25;
		Weapon.BobRangeY 0.25;
		Tag "Luger";
		Weapon.BobSpeed 3.0;
		+WEAPON.NOALERT;
		// +WEAPON.NOAUTOFIRE;
	}
	
	States
	{
		Ready:
			TNT1 A 0 A_JumpIfInventory("PistolSilenced", 1, "ReadySilenced");
			LUG2 A 1 A_WeaponReady;
			Loop;
		ReadySilenced:
			LUG3 A 1 A_WeaponReady;
			Loop;
		Select:
			TNT1 A 0 A_PlaySound("weapons/luger/equip");
		Selecting:
			LUG2 A 1 A_Raise(25);
			Loop;
		Deselect:
			LUG2 A 1 A_Lower(25);
			Loop;
		Fire:
			TNT1 A 0 A_JumpIfInventory("PistolSilenced", 1, "FireSilenced");
			LUG2 BC 1;
			LUG2 D 1 A_LugerFire();
			TNT1 A 0 A_SetPitch(pitch - 0.64, SPF_INTERPOLATE);
			LUG2 EFGH 1;
			LUG2 A 5;	// 5
			Goto Ready;
		FireSilenced:
			LUG3 BC 1;
			LUG3 D 1 A_LugerSilencedFire();
			LUG3 EC 1;
			LUG3 D 1 A_LugerSilencedFire();
			LUG3 EC 1;
			LUG3 D 1 A_LugerSilencedFire();
			LUG3 EFGH 1;
			LUG3 A 7;
			Goto Ready;
		AltFire:
			TNT1 A 0 A_JumpIfInventory("PistolSilenced", 1, "Unsilence");
			TNT1 A 0 A_JumpIfInventory("PistolUnsilenced", 1, "Silence");
		Silence:
			LUGS ABCDEFGHIJKLMNO 1;
			LUGS P 1 A_PlaySound("weapons/luger/silence");
			LUGS QRSTUVWXYZ 1;
			LUGT ABCDEF 1;
			TNT1 A 0
			{
				A_GiveInventory("PistolSilenced", 1);
				A_TakeInventory("PistolUnsilenced", 1);
			}
			Goto ReadySilenced;
		Unsilence:
			LUGT FEDCBA 1;
			LUGS ZYXWVUTSRQ 1;
			LUGS P 1 A_PlaySound("weapons/luger/silence");
			LUGS ONMLKJIHGFEDCBA 1;
			TNT1 A 0
			{
				A_TakeInventory("PistolSilenced", 1);
				A_GiveInventory("PistolUnsilenced", 1);
			}
			Goto Ready;
		Spawn:
			PIST P -1;
			Stop;
	}
}





class SpaceLuger : WolfWeapon
{
	action void A_SpaceLugerFire()
	{
		A_PlaySound("weapons/moonluger/fire");
		// A_FireBullets(1, 1, -1, 10, "BulletPuff1");
		A_FireProjectile("SpaceBullet", 0, 1, 0, 8, 0, 0);
	}

	Default
	{
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "SmallAmmo";
		Weapon.AmmoType2 "SmallAmmo";
		Inventory.PickUpSound "pickups/weapongrab";
		Weapon.BobRangeX 0.25;
		Weapon.BobRangeY 0.25;
		Tag "Luger";
		Weapon.BobSpeed 3.0;
		+WEAPON.NOAUTOFIRE;
	}
	
	States
	{
		Ready:
			MLGR A 1 A_WeaponReady;
			Loop;
		Select:
			TNT1 A 0 A_PlaySound("weapons/luger/equip");
		Selecting:
			MLGR A 1 A_Raise(25);
			Loop;
		Deselect:
			MLGR A 1 A_Lower(25);
			Loop;
		Fire:
			MLGR C 1;
			MLGR D 1 A_SpaceLugerFire();
			TNT1 A 0 A_SetPitch(pitch - 0.8, SPF_INTERPOLATE);
			MLGR E 1;
			Goto Ready;
		Spawn:
			PIST P -1;
			Stop;
	}
}